param unidadeDeTempo;
param dimSquare; /* max number of points - size will be meters * dimSquare */
param dL; /* in meters */
param BIGM;
param tMax;


set P;
param pointDX{P}, >=0, <= dimSquare;
param pointDY{P}, >=0, <= dimSquare;
param pointPackageCAP{P}; /*Vetor real com a quantidade a ser coletada in KG*/
param pointToDeliverDX{P}; /* ponto cordinate Dx*/
param pointToDeliverDY{P}; /* ponto cordinate Dy*/

set T := 1..tMax;

set EP;
param ePDX{EP}, >=0, <= dimSquare;
param ePDY{EP}, >=0, <= dimSquare;

/* param pointEnergyPrice{P,T};*/ /*O preco da energy no instante t*/ 


set DRONE;
param droneInitialDX{DRONE}, >=0, <= dimSquare;
param droneInitialDY{DRONE}, >=0, <= dimSquare;
param droneMS{DRONE}; /*Drone speed in KM/h */
param droneVEC{DRONE}; /*Drone Variable Energy Consumption related to its MaxSpeed (per min) */
param droneFEC{DRONE}; /*Drone Fixed Energy Consumption (per min)  */
param droneMaxCap{DRONE}; /* Drone max cap in kg */
param droneDoD{DRONE}; /*Maximo que se pode descarregar*/
param droneRateOfCharge{DRONE}; /* Drone Rate of Charge (per min)*/
param droneBROnArr{DRONE}; /* Drone battery rate on Arrival (%)*/
param droneIsDeliveringAtBegin{DRONE,P}, binary; /* Time-dependent matrix indication who is delivering at the begin*/
param dronesMinFinalCharge; /* Hard constrain for ensuring a minimum amount of energy in each uav battery*/

/* DECISION VARIAVEIS*/
var droneDX{DRONE, T}, >=0, <= dimSquare;
var droneDY{DRONE, T}, >=0, <= dimSquare;
var droneCurrentCap{DRONE, T}, >=0;
var droneCollected{DRONE, P, T}, binary;
var droneDelivered{DRONE, P, T}, binary;
var droneIsDelivering{DRONE, P, T}, binary;
var droneIsChargingAtEP{DRONE,T, EP}, binary;

/*AUXILIAR VARIABLES*/
var diffPosX{DRONE, T}, >=0;
var diffNegX{DRONE, T}, >=0;
var diffPosY{DRONE, T}, >=0;
var diffNegY{DRONE, T}, >=0;
var timePerProductDelivered{P}, >=0;
var droneSpeed{DRONE,T}, >=0;
var droneBatteryRate{DRONE,T}, >=0, <=100;
var excessOfEnergy{DRONE,T}, >=0, <=20;
var droneIsOn{DRONE,T}, binary;
var droneIsUsed{DRONE}, binary;

/*OBJ FUNCTION - AUXILIARY VARIABLES*/
var totalDist, >=0;
var timeToDeliver, >=0;
var nUsedDrones, >=0;
var dronesMaxSpeed, >=0;
var maximizeFinalCharge, >=0;
var makeSpanLC, >=0; 
var makeSpanLD, >=0; 
var auxTotalExcessOfEnergy, >=0;

s.t. 

calcTotalDist: totalDist = sum{t in T:t>=2} sum{d in DRONE} (diffNegX[d,t] + diffPosX[d,t] + diffPosY[d,t] + diffNegY[d,t]) * dL / 1000;
calcTotalTimeToDeliver: timeToDeliver = sum{p in P} timePerProductDelivered[p];
calcNDrones: nUsedDrones = sum{d in DRONE} droneIsUsed[d];
calcDronesAVGSpeed{d in DRONE, t in T:t>=2}: dronesMaxSpeed >= droneSpeed[d,t];
calcBatteriesOverLoad: auxTotalExcessOfEnergy = sum{d in DRONE} sum{t in T} excessOfEnergy[d,t];

minimize obj: totalDist + timeToDeliver + nUsedDrones + dronesMaxSpeed + maximizeFinalCharge + makeSpanLC + makeSpanLD + auxTotalExcessOfEnergy;

respectDroneDoD{d in DRONE, t in T:t>=2}: droneBatteryRate[d,t] >= droneDoD[d];
respectDroneMinFinalCharge{d in DRONE}: droneBatteryRate[d,tMax] >= dronesMinFinalCharge;
respectDroneCap{d in DRONE, t in T}: droneCurrentCap[d,t] <= droneMaxCap[d];

calcEnergyToMakeBatteryFull: maximizeFinalCharge = sum{d in DRONE} ( 100 - droneBatteryRate[d,tMax]);

updatedCAP{d in DRONE, t in T:t>=2}:  droneCurrentCap[d,t] = droneCurrentCap[d,t-1] + sum{p in P} ( droneCollected[d,p,t]*pointPackageCAP[p] - droneDelivered[d,p,t]*pointPackageCAP[p]);
updatedCAPAtBegin{d in DRONE}:  droneCurrentCap[d,1] = sum{p in P} droneIsDeliveringAtBegin[d,p]*pointPackageCAP[p];


/* droneShouldCollect{p in P}: (sum{d in DRONE} droneIsDeliveringAtBegin[d,p] + (sum{t in T} sum{d in DRONE} droneCollected[d,p,t]) )*pointPackageCAP[p] >= pointPackageCAP[p]; */

/* maybe remove second term */
droneShouldCollectAlone{p in P}: (sum{d in DRONE} droneIsDeliveringAtBegin[d,p]) + (sum{t in T} sum{d in DRONE} droneCollected[d,p,t]) = 1;
droneShouldCollectAndDeliver{p in P, d in DRONE}: sum{t in T} droneDelivered[d,p,t] = droneIsDeliveringAtBegin[d,p] + (sum{t in T} droneCollected[d,p,t]); 


verifyLastC{p in P}:  makeSpanLC >= sum{d in DRONE} sum{t in T} droneCollected[d,p,t]*t; 
verifyLastD{p in P}:  makeSpanLD >= sum{d in DRONE} sum{t in T} droneDelivered[d,p,t]*t; 

updatedDelivering{p in P, d in DRONE,t in T:t>=2}: droneIsDelivering[d,p,t] = droneIsDelivering[d,p,t-1] + droneCollected[d,p,t] - droneDelivered[d,p,t];
updatedDeliveringAtBegin{p in P, d in DRONE,t in T:t==1}: droneIsDelivering[d,p,t] = droneIsDeliveringAtBegin[d,p];
droneShouldBeDoingNothingAtTheEnd{p in P, d in DRONE, t in T:t==tMax}: droneIsDelivering[d,p,t] = 0;

droneIsBeingUsed{d in DRONE}: droneIsUsed[d]*BIGM >= sum{p in P} sum{t in T} droneIsDelivering[d,p,t];
updateDroneIsOn{d in DRONE, t in T}: droneIsOn[d,t]*BIGM >= droneSpeed[d,t];

calcBaterryRateFirstInterval{d in DRONE}: droneBatteryRate[d,1] = droneBROnArr[d];
calcBaterryRate{d in DRONE, t in T:t>=2}: droneBatteryRate[d,t] = droneBatteryRate[d,t-1] - (droneVEC[d]*droneSpeed[d,t])/droneMS[d]*unidadeDeTempo - droneIsOn[d,t]*droneFEC[d]*unidadeDeTempo + sum{ep in EP} (droneIsChargingAtEP[d,t,ep]*droneRateOfCharge[d])*unidadeDeTempo - excessOfEnergy[d,t];

productDeliveringTime{p in P}: timePerProductDelivered[p] = sum{d in DRONE} sum{t in T} droneIsDelivering[d,p,t];

initializePositionX{d in DRONE}: droneDX[d,1] = droneInitialDX[d];
initializePositionY{d in DRONE}: droneDY[d,1] = droneInitialDY[d];
                                                                    
packageWasCollectedAtRightPositionX1{p in P, d in DRONE,t in T}:  droneDX[d,t]  - pointDX[p] <=  dimSquare*(1 - droneCollected[d,p,t]);
packageWasCollectedAtRightPositionX2{p in P, d in DRONE,t in T}:  -droneDX[d,t]  + pointDX[p]  <=  dimSquare*(1 - droneCollected[d,p,t]);
packageWasCollectedAtRightPositionY1{p in P, d in DRONE,t in T}:  droneDY[d,t]  - pointDY[p] <=  dimSquare*(1 - droneCollected[d,p,t]);
packageWasCollectedAtRightPositionY2{p in P, d in DRONE,t in T}:  -droneDY[d,t]  + pointDY[p]   <=  dimSquare*(1 - droneCollected[d,p,t]);


packageWasDeliveredAtRightPositionX1{p in P, d in DRONE,t in T}:  droneDX[d,t]  - pointToDeliverDX[p] <=  dimSquare*(1 - droneDelivered[d,p,t]);
packageWasDeliveredAtRightPositionX2{p in P, d in DRONE,t in T}:  -droneDX[d,t]  + pointToDeliverDX[p] <=  dimSquare*(1 - droneDelivered[d,p,t]);
packageWasDeliveredAtRightPositionY1{p in P, d in DRONE,t in T}:  droneDY[d,t]  - pointToDeliverDY[p] <=  dimSquare*(1 - droneDelivered[d,p,t]);
packageWasDeliveredAtRightPositionY2{p in P, d in DRONE,t in T}:  -droneDY[d,t]  + pointToDeliverDY[p] <=  dimSquare*(1 - droneDelivered[d,p,t]);

droneChargedAtRightPositionX1{ep in EP, d in DRONE,t in T}:  droneDX[d,t]  - ePDX[ep] <=  dimSquare*(1 - droneIsChargingAtEP[d,t,ep]);
droneChargedRightPositionX2{ep in EP, d in DRONE,t in T}:  -droneDX[d,t]  + ePDX[ep]  <=  dimSquare*(1 - droneIsChargingAtEP[d,t,ep]);
droneChargedAtRightPositionY1{ep in EP, d in DRONE,t in T}:  droneDY[d,t]  - ePDY[ep] <=  dimSquare*(1 - droneIsChargingAtEP[d,t,ep]);
droneChargedAtRightPositionY2{ep in EP, d in DRONE,t in T}:  -droneDY[d,t]  + ePDY[ep]   <=  dimSquare*(1 - droneIsChargingAtEP[d,t,ep]);

calcABSMoveX1{d in DRONE,t in T:t>=2}: (droneDX[d,t] - droneDX[d,t-1]) + diffPosX[d,t] >=  0;
calcABSMoveX2{d in DRONE,t in T:t>=2}: (droneDX[d,t] - droneDX[d,t-1]) - diffNegX[d,t] <=  0;
calcABSMoveY1{d in DRONE,t in T:t>=2}: (droneDY[d,t] - droneDY[d,t-1]) + diffPosY[d,t] >=  0;
calcABSMoveY2{d in DRONE,t in T:t>=2}: (droneDY[d,t] - droneDY[d,t-1]) - diffNegY[d,t] <=  0;


calcDronesSpeed{d in DRONE, t in T:t>=2}:  droneSpeed[d,t] = ( (diffPosX[d,t] + diffPosX[d,t] + diffPosY[d,t] + diffNegY[d,t])*dL / unidadeDeTempo * 60 / 1000);

respectDroneMaxSpeed{d in DRONE, t in T:t>=2}:  droneSpeed[d,t] <= droneMS[d];
